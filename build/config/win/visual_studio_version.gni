# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

_programfiles_x86 = getenv("ProgramFiles(x86)")
_default_windows_sdk_path = getenv("WINDOWSSDKDIR")
if (_default_windows_sdk_path == "") {
  _default_windows_sdk_path = "$_programfiles_x86\Windows Kits\10"
}

declare_args() {
  # Path to Visual Studio. If empty, the default is used which is to use the
  # the default location for Visual Studio 2015. If set, you must also set the
  # visual_studio_version.
  visual_studio_path = getenv("VISUAL_STUDIO_PATH")

  # Version of Visual Studio pointed to by the visual_studio_path.
  # Currently always "2015".
  visual_studio_version = getenv("VISUAL_STUDIO_VERSION")

  # Full path to the Windows SDK, not including a backslash at the end.
  # This value is the default location, override if you have a different
  # installation location.
  windows_sdk_path = _default_windows_sdk_path
}

if (visual_studio_path == "") {
  toolchain_data =
      exec_script("../../vs_toolchain.py", [ "get_toolchain_dir" ], "scope")
  visual_studio_path = toolchain_data.vs_path
  visual_studio_version = toolchain_data.vs_version
  visual_studio_runtime_dirs = toolchain_data.runtime_dirs
  windows_sdk_path = toolchain_data.sdk_path
} else {
  assert(visual_studio_version != "",
         "You must set the visual_studio_version if you set the path")
  visual_studio_runtime_dirs = ""
}
